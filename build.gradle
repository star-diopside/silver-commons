plugins {
    id "org.springframework.boot" version "2.1.0.RELEASE" apply false
    id "com.github.spotbugs" version "1.6.5"
}

allprojects {
    group = "jp.gr.java_conf.stardiopside.silver.commons"
    version = "1.0.2-SNAPSHOT"

    ext {
        dbunitVersion = "2.5.3"
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "checkstyle"
    apply plugin: "com.github.spotbugs"
    apply plugin: "jacoco"

    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        jcenter()
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    javadoc {
        options.charSet = "UTF-8"
        options.encoding = "UTF-8"
        options.addBooleanOption("html5", true)
    }

    checkstyle {
        toolVersion = "8.14"
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
        ignoreFailures = true
    }

    spotbugs {
        toolVersion = "3.1.8"
        ignoreFailures = true
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    jacoco {
        toolVersion = "0.8.2"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = "javadoc"
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

project(":silver-commons-support") {
    dependencies {
        api "org.springframework:spring-aop"
        implementation "org.apache.commons:commons-lang3"
        compileOnly "org.slf4j:jcl-over-slf4j"
    }
}

project(":silver-commons-batch") {
    dependencies {
        api project(":silver-commons-support")
        api "org.springframework.batch:spring-batch-core"
        implementation "org.apache.commons:commons-lang3"
    }
}

project(":silver-commons-web") {
    dependencies {
        api "org.springframework:spring-context"
        api "org.springframework:spring-web"
        implementation "org.apache.commons:commons-lang3"
        compileOnly "javax.servlet:javax.servlet-api"
        compileOnly "javax.validation:validation-api"
    }
}

project(":silver-commons-test") {
    dependencies {
        api "org.dbunit:dbunit:${dbunitVersion}"
        implementation "org.springframework:spring-tx"
        testImplementation "junit:junit"
        testImplementation "org.hamcrest:hamcrest-library"
        testImplementation "ch.qos.logback:logback-classic"
    }
}
